# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Bartłomiej Piotrowski <barthalion@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>

pkgname=libaio
pkgver=0.3.113
pkgrel=5
pkgdesc="The Linux-native asynchronous I/O facility (aio) library"
arch=(x86_64)
url="https://codeberg.org/jmoyer/libaio"
license=(LGPL-2.0-or-later)
depends=(glibc)
provides=(libaio.so)
# LTO is not supported: https://codeberg.org/jmoyer/libaio/issues/10
options=(!lto)
source=($url/archive/$pkgname-$pkgver.tar.gz)
sha512sums=('9544f595fe325a37df2afd6369ce348005202db47363587cad29d46522304dc7e733a4cd195d9c5b724a2d4f225834b10a30307efdd1b2df245dfe2f7c18a9a2')
b2sums=('dc86b25c8487cdbaa57ebc70d699c6c90509ba0ea86672b33624892a4f3245d857031f104d0024d10d2fbbdb149b189c548e6f9a6e53ebc3cd01bad13cb5c5c1')

prepare() {
  # -Werror, not even once
  sed 's/-Werror//' -i $pkgname/harness/Makefile

  # remove failing tests until upstream fixes them
  rm -frv $pkgname/harness/cases/20.t
  rm -frv $pkgname/harness/cases/21.t
}

build() {
  make -C $pkgname
}

check() {
  make partcheck -k -C $pkgname
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname
  install -vDm 644 $pkgname/{ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname/man/*.3 -t "$pkgdir/usr/share/man/man3/"
}

