# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kbd
pkgver=2.10.0
pkgrel=1
pkgdesc="Keytable files and keyboard utilities"
arch=('x86_64')
url="http://www.kbd-project.org"
license=('GPL-2.0-or-later')
depends=(
  glibc
  gzip
  pam
)
makedepends=(
  check
  git
)
source=(
  git+https://git.kernel.org/pub/scm/linux/kernel/git/legion/kbd.git?signed#tag=v$pkgver
  fix-euro2.patch
  vlock.pam
)
backup=('etc/pam.d/vlock')
provides=('vlock')
conflicts=('vlock')
replaces=('vlock')
b2sums=('e98958b6bd29c1ffe9a22ad2e466db50edebf6f8fbe403b59e70f3a5c44adc11e3948d43a59859b13558107bcc1e63153be2b784bcd8616268fb08bf3f3a5e1d'
        'd122ddb1a86e7a282df8e438903f94d697e3d18a24154d976334e6b54b8f1cf1df432cf8dbcd98daa55014ada462f284d0319fbf015554266e91f4d2a8bf812b'
        '104543e72331a633572a26059e6dce1f25c3c8d6deabb855dd94bfffb72edf8a53a58c6ea7ef6806dd80bcd6ab0aa47cc1a45cc0cd90330be6514ff7591b5140')
validpgpkeys=(
  '7F2A3D07298149A0793C9A4EA45ABA544CFFD434' #Alexey Gladkov 
)

prepare() {
  cd "${pkgname}"
  # rename keymap files with the same names
  # this is needed because when only name of keymap is specified
  # loadkeys loads the first keymap it can find, which is bad (see FS#13837)
  # this should be removed when upstream adopts the change
  mv data/keymaps/i386/qwertz/cz{,-qwertz}.map
  mv data/keymaps/i386/olpc/es{,-olpc}.map
  mv data/keymaps/i386/olpc/pt{,-olpc}.map
  mv data/keymaps/i386/fgGIod/trf{,-fgGIod}.map
  mv data/keymaps/i386/colemak/{en-latin9,colemak}.map
  # fix euro2 #28213
  patch -Np1 -i ../fix-euro2.patch
  autoreconf -if
}

build() {
  cd "${pkgname}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --datadir=/usr/share/kbd \
              --mandir=/usr/share/man \
              --enable-optional-progs \
              --disable-tests
  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes
}

package() {
  cd "${pkgname}"
  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes DESTDIR="${pkgdir}" install
  install -Dm644 ../vlock.pam "${pkgdir}"/etc/pam.d/vlock
}
