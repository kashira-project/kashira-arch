# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=imath
pkgver=3.2.3
pkgrel=1
pkgdesc='A C++ and python library of 2D and 3D vector, matrix, and math operations for computer graphics'
url='https://www.openexr.com/'
arch=(x86_64)
license=(BSD-3-Clause)
depends=(glibc
         libgcc
         libstdc++)
optdepends=('boost-libs: python bindings'
            'python: python bindings')
makedepends=(boost
             boost-libs
             cmake
             git
             python
             python-numpy)
source=(git+https://github.com/AcademySoftwareFoundation/Imath#tag=v$pkgver)
sha256sums=('995eeb88f9ccc5d52c6ff91536ceafb7ca63b2284c0dac9830386df45d4490ec')

build() {
  cmake -B build -S Imath \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 Imath/LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
