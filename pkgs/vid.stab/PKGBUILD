# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: EVorster <evorster@gmail.com>

pkgname=vid.stab
pkgver=1.1.2
pkgrel=1
pkgdesc='Video stabilization library'
arch=(x86_64)
url=http://public.hronopik.de/vid.stab
license=(GPL-2.0-or-later)
depends=(
  glibc
  libgcc
  libgomp
)
makedepends=(
  cmake
  git
  ninja
)
provides=(libvidstab.so)
source=(git+https://github.com/georgmartius/vid.stab.git#tag=v${pkgver})
b2sums=('61e32f89adb31d25180f061b61364f00cb9afc602f584037d1c4357a21e5707e92b901a96fb1201d03c326ae478be3dbdac69a181d14d0664f1c5a1534c85a60')

pkgver() {
  cd vid.stab
  git describe --tags | sed 's/^v//'
}

build() {
  cmake -S vid.stab -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
