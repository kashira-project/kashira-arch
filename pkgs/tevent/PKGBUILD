# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Marco A Rojas <marquicus at gmail dot com>
# Contributor: Ng Oon-Ee <ngoonee.talk@gmail.com>
# Contributor: Thomas Burdick <thomas.burdick@gmail.com>

pkgname=tevent
pkgver=0.17.2
pkgrel=2
epoch=1
pkgdesc='Event system based on the talloc memory management library'
url="https://tevent.samba.org/"
arch=('x86_64')
source=(https://samba.org/ftp/tevent/${pkgname}-${pkgver}.tar{.gz,.asc})
license=('GPL-3.0-or-later')
depends=('talloc')
makedepends=('python' 'cmocka')
optdepends=('python: for python bindings')
provides=(libtevent.so)
validpgpkeys=('9147A339719518EE9011BCB54793916113084025') # Samba Library Distribution Key <samba-bugs@samba.org>
b2sums=('973d5cd072348436f960ead5cb150cf3974a4398d400a4d9b74ece04bdab899fbd646bfd9b3b0640fe96ce09acb01b477aba8235a6910ce93c9bd2157cb3fdfb'
        'SKIP')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --bundled-libraries=NONE \
    --disable-rpath-install \
    --builtin-libraries=replace
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
