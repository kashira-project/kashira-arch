# Maintainer: Sébastien Luttringer

pkgname=libnftnl
pkgver=1.3.2
pkgrel=1
pkgdesc='Netfilter library providing interface to the nf_tables subsystem'
arch=('x86_64')
url='https://netfilter.org/projects/libnftnl/'
license=('GPL-2.0-or-later')
depends=('glibc' 'libmnl')
makedepends=('git')
validpgpkeys=('8C5F7146A1757A65E2422A94D70D1A666ACF2B21') # Netfilter Core Team
source=("git+https://git.netfilter.org/libnftnl.git?signed#tag=${pkgname}-${pkgver}")
sha256sums=('1cc163418e3d49900cde95056572217cd51bce975872da18146dd2e95180a32b')

prepare() {
  cd $pkgname
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
