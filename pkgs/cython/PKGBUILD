# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Igor Scabini <furester @ gmail.com>

pkgname=cython
pkgver=3.3.0
pkgrel=1
pkgdesc='C-Extensions for Python'
arch=(x86_64)
url='https://cython.org'
license=(Apache-2.0)
depends=(glibc
         python
         python-numpy
         python-pygments
         python-setuptools)
makedepends=(git
             python-build
             python-installer
             python-wheel)
checkdepends=(gdb
              python-interpreters-pep-734
              python-pytest
              python-tests)
source=(git+https://github.com/cython/cython#tag=$pkgver)
sha256sums=('89b5fcbb67c01e6c8fd079da74be1290c6f36ef10201dd7281eee8d6c17abe41')

prepare() {
  cd cython
  git cherry-pick -n 2ea7297e5f732e1bfa6e562662df145170bb0bfc # Fix int comparison with GCC+LTO
}

build() {
  cd cython
  python -m build --wheel --no-isolation
}

check() {
  cd cython
  python runtests.py -vv -j 64 --no-pyregr
}

package() {
  cd cython
  python -m installer --destdir="$pkgdir" dist/*.whl

  for f in cygdb cython cythonize; do
    mv "$pkgdir"/usr/bin/$f "$pkgdir"/usr/bin/${f}3
    ln -s ${f}3 "$pkgdir"/usr/bin/$f
  done
}
